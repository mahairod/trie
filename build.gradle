plugins {
	id 'java'
}

group = 'io.elliptica'
version = '1.0-SNAPSHOT'

repositories {
	mavenCentral()
}

sourceSets {
	main
	data
	jmh {
		compileClasspath += main.runtimeClasspath
		compileClasspath += data.runtimeClasspath
	}
	test {
		compileClasspath += data.runtimeClasspath
	}
}

configurations {
	jmhImplementation {
		extendsFrom(implementation)
	}
}

task jmh(type: JavaExec, dependsOn: [jmhClasses,classes]) {
	mainClass = 'org.openjdk.jmh.Main'
	classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
	args += ["-jvmArgsAppend",  "-server",
//			 "ru.elliptica.collections.TrieBench.benchTrieSearchVocab"
	]
//	debug = true
}

task jmhProf(type: JavaExec, dependsOn: [jmhClasses,classes]) {
	workingDir = file("$buildDir/reports/profiling")
	doFirst {
		workingDir.mkdirs()
	}
	mainClass = 'org.openjdk.jmh.Main'
	args += ["-prof", "jfr", "-jvmArgsAppend",  "-server"]

	classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

testClasses.dependsOn(dataClasses)

compileJava {
//	options.compilerArgs << ""
}

tasks.withType(JavaExec) {
	jvmArgs += [
		"--add-opens", "java.base/java.lang=ALL-UNNAMED",
		"--add-opens", "java.base/java.util=ALL-UNNAMED"
	]
}

classes {
	finalizedBy(jmhClasses)
}

dependencies {
	jmhImplementation('org.openjdk.jmh:jmh-core:1.37')
	jmhImplementation('org.openjdk.jmh:jmh-generator-annprocess:1.37')
//	jmhImplementation('org.openjdk.jmh:jmh-generator-reflection:1.37')

	jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'

	testImplementation platform('org.junit:junit-bom:5.10.0')
	testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
	useJUnitPlatform()
	jvmArgs += [
		"--add-opens", "java.base/java.lang=ALL-UNNAMED",
		"--add-opens", "java.base/java.util=ALL-UNNAMED"
	]
}

wrapper {
    gradleVersion = '8.7'
}
